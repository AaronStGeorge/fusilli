# Copyright 2025 Advanced Micro Devices, Inc.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions.
# See https://llvm.org/LICENSE.txt for license information.
# SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception

include(GoogleTest)
find_package(Threads REQUIRED)

# Plugin API tests
add_executable(test_fusilli_plugin_api
    test_fusilli_plugin_api.cpp
    utils.h
)
target_compile_options(test_fusilli_plugin_api PRIVATE ${FUSILLI_PLUGIN_WARNING_COMPILE_OPTIONS})
target_link_libraries(test_fusilli_plugin_api PRIVATE
    fusilli_plugin  # Link plugin directly
    fusilli::fusilli
    GTest::gtest_main
    hip::host
    hipdnn_sdk
    Threads::Threads
)
target_compile_definitions(test_fusilli_plugin_api PRIVATE
  FUSILLI_PLUGIN_NAME="${FUSILLI_PLUGIN_NAME}"
  FUSILLI_PLUGIN_ENGINE_ID=${FUSILLI_PLUGIN_ENGINE_ID}
)
# Register with CTest
gtest_discover_tests(test_fusilli_plugin_api
    PROPERTIES
      ENVIRONMENT "HIPDNN_LOG_LEVEL=info"
      ENVIRONMENT "FUSILLI_LOG_INFO=1"
      ENVIRONMENT "FUSILLI_LOG_FILE=stdout"
)

# Install test executable
install(TARGETS test_fusilli_plugin_api
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)

# Graph import tests
add_executable(test_graph_import
    test_graph_import.cpp
    utils.h
)
target_compile_options(test_graph_import PRIVATE ${FUSILLI_PLUGIN_WARNING_COMPILE_OPTIONS})
target_link_libraries(test_graph_import PRIVATE
    fusilli::fusilli
    GTest::gtest_main
    hipdnn_sdk
)
# Register with CTest
gtest_discover_tests(test_graph_import
    PROPERTIES
      ENVIRONMENT "HIPDNN_LOG_LEVEL=info"
      ENVIRONMENT "FUSILLI_LOG_INFO=1"
      ENVIRONMENT "FUSILLI_LOG_FILE=stdout"
)

# Install test executable
install(TARGETS test_graph_import
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)

# Integration tests
add_subdirectory(integration)

# Generate and install CTestTestfile.cmake for installed tests
# This allows running ctest from the installed test directory
set(FUSILLI_PLUGIN_TEST_INSTALL_DIR "${CMAKE_INSTALL_BINDIR}/fusilli_plugin")
set(INSTALLED_CTEST_FILE "${CMAKE_CURRENT_BINARY_DIR}/CTestTestfile.cmake.install")

# Generate CTestTestfile.cmake that references installed test executables
file(WRITE "${INSTALLED_CTEST_FILE}"
    "# Autogenerated CTestTestfile for installed fusilli_plugin tests\n"
    "# Generated by fusilli_plugin build system\n\n"
)

# Add tests to the generated CTestTestfile.cmake
# Test paths are relative to the install directory
file(APPEND "${INSTALLED_CTEST_FILE}"
    "add_test(test_fusilli_plugin_api \"../test_fusilli_plugin_api\")\n"
)
file(APPEND "${INSTALLED_CTEST_FILE}"
    "add_test(test_graph_import \"../test_graph_import\")\n"
)
file(APPEND "${INSTALLED_CTEST_FILE}"
    "add_test(fusilli_plugin_integration_tests \"../fusilli_plugin_integration_tests\")\n"
)

# Set test properties for environment variables if needed
file(APPEND "${INSTALLED_CTEST_FILE}"
    "\n# Set environment variables for all tests\n"
    "set_tests_properties(test_fusilli_plugin_api test_graph_import fusilli_plugin_integration_tests\n"
    "    PROPERTIES\n"
    "      ENVIRONMENT \"HIPDNN_LOG_LEVEL=info\"\n"
    "      ENVIRONMENT \"FUSILLI_LOG_INFO=1\"\n"
    "      ENVIRONMENT \"FUSILLI_LOG_FILE=stdout\"\n"
    ")\n"
)

# Install the generated CTestTestfile.cmake
install(
    FILES "${INSTALLED_CTEST_FILE}"
    DESTINATION ${FUSILLI_PLUGIN_TEST_INSTALL_DIR}
    RENAME CTestTestfile.cmake
)
